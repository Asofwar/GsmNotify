// Top-level build file where you can add configuration options common to all sub-projects/modules.

plugins {
    id 'com.android.application' version '8.5.1'
}

def userHome = System.getProperty('user.home')
def debugKeystore = file("${userHome}/.android/debug.keystore")
def releaseKeystoreFile = file("/home/adonai/devel/keystore/Adonai")

android {
    namespace 'com.adonai.GsmNotify'
    compileSdk 35

    defaultConfig {
        applicationId "com.adonai.GsmNotify"
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 222
        versionName "2.2.2"
    }

    def releaseKeystore = file("/home/adonai/devel/keystore/Adonai")

    signingConfigs {
        debug {
            storeFile debugKeystore
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            if (releaseKeystoreFile.exists()) {
                storeFile releaseKeystoreFile
                storePassword "release"
                keyAlias "adonai"
                keyPassword "release"
            } else {
                initWith signingConfigs.debug
                enableV1Signing = true
                enableV2Signing = true
                println "[GsmNotify] Release keystore not found at ${releaseKeystoreFile}. Falling back to debug signing keys for CI builds."
            }
        }
    }

    buildTypes {
        debug {
//            minifyEnabled true
//            shrinkResources true
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }

        release {
            debuggable false
            signingConfig signingConfigs.release
        }

        pro {
            debuggable false
            applicationIdSuffix '.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def ensureDebugKeystore = tasks.register("ensureDebugKeystore") {
    doLast {
        if (!debugKeystore.exists()) {
            debugKeystore.parentFile.mkdirs()

            def javaHome = System.getenv("JAVA_HOME")
            def keytoolCandidate = javaHome ? new File(javaHome, "bin/keytool") : null
            def keytoolExecutable = keytoolCandidate?.exists() ? keytoolCandidate.absolutePath : "keytool"

            println "[GsmNotify] Generating debug keystore at ${debugKeystore}"
            project.exec {
                executable keytoolExecutable
                args "-genkeypair",
                        "-alias", "androiddebugkey",
                        "-keypass", "android",
                        "-keystore", debugKeystore.absolutePath,
                        "-storepass", "android",
                        "-dname", "CN=Android Debug,O=Android,C=US",
                        "-keyalg", "RSA",
                        "-keysize", "2048",
                        "-validity", "10000"
            }
        }
    }
}

tasks.configureEach { task ->
    if (task.name == "validateSigningDebug") {
        task.dependsOn ensureDebugKeystore
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core:1.13.1'
    implementation 'androidx.fragment:fragment:1.7.1'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.code.gson:gson:2.13.2'                             // Json (De)Serializer
    implementation 'com.j256.ormlite:ormlite-android:6.1'
    implementation 'com.github.BlackBoxVision:datetimepicker-edittext:v0.3.3'
}
